apply plugin: "groovy"
apply plugin: "distribution"
apply plugin: "maven-publish"

ext {
  artifactBaseName = "buildcommon"
  artifactorySnapshotRepoUrl = "$artifactoryBaseUrl/libs-snapshot-local"
  artifactoryReleaseRepoUrl = "$artifactoryBaseUrl/libs-release-local"
}

group = "com.recursiveloop.gradle"
version = "0.1.1-SNAPSHOT"

dependencies {
  compile gradleApi()
  compile localGroovy()
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.6"
}

task sourcesJar(type: Jar) {
  baseName artifactBaseName
  classifier "sources"
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  baseName artifactBaseName
  classifier "groovydoc"
  from groovydoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives groovydocJar
}

distributions {
  main {
    baseName = artifactBaseName

    contents {
      from { libsDir }
    }
  }
}

publishing {
  publications {
    buildcommon(MavenPublication) {
      from components.java
      artifactId "buildcommon"

      artifact sourcesJar
      artifact groovydocJar
    }
  }

  repositories {
    maven {
      name "artifactory"
      url project.version.endsWith("-SNAPSHOT") ?
        artifactorySnapshotRepoUrl : artifactoryReleaseRepoUrl

      credentials {
        username = artifactoryUsername
        password = artifactoryPassword
      }
    }
  }
}
